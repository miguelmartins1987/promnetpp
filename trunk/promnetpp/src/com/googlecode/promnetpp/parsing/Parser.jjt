/*
 * Copyright (c) 2013, Miguel Martins
 * Use is subject to license terms.
 *
 * This source code file is provided under the MIT License. Full licensing
 * terms should be available in the form of text files. The standard source code
 * distribution provides a LICENSE.txt file which can be consulted for licensing
 * details.
 */
 
options {
    DEBUG_LOOKAHEAD=false;
    DEBUG_PARSER=false;
    LOOKAHEAD=3;
    NODE_CLASS="ASTNode";
    STATIC=false;
}

PARSER_BEGIN(PROMELAParser)

package com.googlecode.promnetpp.parsing;

import java.io.BufferedReader;
import java.io.InputStreamReader;

public class PROMELAParser {
    public static void main(String[] args) throws Exception {
        BufferedReader standardInputReader = new BufferedReader(
            new InputStreamReader(System.in));
        PROMELAParser parser = new PROMELAParser(standardInputReader);
        System.err.println("Waiting for your input.");
        ASTNode rootNode = parser.Start();
        rootNode.dump("");
    }
}

PARSER_END(PROMELAParser)

SKIP : {" " | "\t" | "\r" | "\n"}

/* Comments */
MORE : {
    "/*": IN_MULTI_LINE_COMMENT
}

<IN_MULTI_LINE_COMMENT> MORE : {
    < ~[] >
}

<IN_MULTI_LINE_COMMENT> TOKEN : {
    <MULTI_LINE_COMMENT: "*/" > : DEFAULT
}
/* Comment handling ends here */

/* Define directives*/
MORE : {
    "#define": IN_DEFINE
}

<IN_DEFINE> MORE : {
    < ~["\n"] >
}

<IN_DEFINE> TOKEN : {
    < DEFINE_DIRECTIVE : "\n" > : DEFAULT
}
/* End define directives*/

TOKEN : {
    < #LETTER: ["_","a"-"z","A"-"Z"] >
    | < #DIGIT: ["0"-"9"] >
    /* Keywords */
    | < ATOMIC : "atomic" >
    | < BIT : "bit">
    | < BOOL : "bool">
    | < BREAK : "break" >
    | < BYTE : "byte">
    | < CHAN : "chan" >
    | < D_STEP : "d_step" >
    | < DO : "do">
    | < ELSE : "else" >
    | < FALSE : "false" >
    | < FI : "fi">
    | < FOR : "for">
    | < IF : "if">
    | < INIT : "init" >
    | < INLINE : "inline" >
    | < INT : "int" >
    | < MTYPE : "mtype" >
    | < OD : "od" >
    | < OF : "of" >
    | < PROCTYPE : "proctype" >
    | < RUN : "run" >
    | < SHORT : "short" >
    | < SKIP_ : "skip" >
    | < TIMEOUT : "timeout" >
    | < TRUE : "true" >
    | < TYPEDEF : "typedef" >
    /* Literals */
    | < INTEGER_LITERAL: (<DIGIT>)+ >
    | < STRING_LITERAL:
      "\""
      (   (~["\"","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )*
      "\""
    >
    /* Other */
    | < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
}

ASTNode Start() : {}
{
    Specification() <EOF>
    { return jjtThis; }
}

void Specification() : {}
{
    (Unit())+
}

void Unit() : {}
{
    Comment()
    | DefineDirective()
    | TypeDefinition()
    | GlobalDeclaration()
    | FunctionDefinition()
    | ProcessDefinition()
    | InitProcessDefinition()
}

void Comment() : {Token comment;}
{
    comment = <MULTI_LINE_COMMENT> { jjtThis.value = comment.image; }
}

void DefineDirective() : {Token directive;}
{
    directive = <DEFINE_DIRECTIVE> { jjtThis.value = directive.image; }
}

void TypeDefinition() : {Token identifier;}
{
    <TYPEDEF> identifier = <IDENTIFIER> { jjtThis.name = identifier.image; }
    "{" SimpleDeclaration() (";" SimpleDeclaration())* [";"] "}" [";"]
}

void SimpleDeclaration() : {String typeName; Token identifier;
Token arrayCapacity;}
{
    typeName = TypeName() identifier = <IDENTIFIER> 
    { jjtThis.typeName = typeName; jjtThis.name = identifier.image; }
    /* Is this an array declaration? */
    [ "["
    (arrayCapacity = <IDENTIFIER> | arrayCapacity = <INTEGER_LITERAL>)
    "]" { jjtThis.putValue("isArray", true); jjtThis.putValue("arrayCapacity",
    arrayCapacity.image); }
    ]
}

void MultiDeclaration() : {}
{
    TypeName() <IDENTIFIER> ("," <IDENTIFIER>)+
}

String TypeName() : {Token identifier;}
{
    <BIT> { jjtThis.name = "bit"; return "bit"; }
    | <BOOL> { jjtThis.name = "bool"; return "bool"; }
    | <BYTE> { jjtThis.name = "byte"; return "byte"; }
    | <CHAN> { jjtThis.name = "chan"; return "chan"; }
    | <INT> { jjtThis.name = "int"; return "int"; }
    | <MTYPE> { jjtThis.name = "mtype"; return "mtype"; }
    | <SHORT> { jjtThis.name = "short"; return "short"; }
    | identifier = <IDENTIFIER> { jjtThis.name = identifier.image;
        return identifier.image; }
}

void GlobalDeclaration() : {}
{
    SimpleDeclaration() [ "=" (Expression() | ChannelInitialization()) ] ";"
}

void ChannelInitialization() : {Token capacity;}
{
    "[" (capacity = <IDENTIFIER> | capacity = <INTEGER_LITERAL>) "]"
    { jjtThis.putValue("capacity", capacity.image); }
    <OF> "{" TypeName() ("," TypeName())* "}"
}

void FunctionDefinition() : {Token functionName;}
{
    <INLINE> functionName = <IDENTIFIER>
    { jjtThis.putValue("functionName", functionName.image); }
    "(" [FunctionParameters()] ")"
    "{" InstructionList() "}"
}

void FunctionParameters() : {}
{
    <IDENTIFIER> ("," <IDENTIFIER>)*
}

void InstructionList() : {}
{
    (Instruction())+
}

void Instruction() : {}
{
    NonBlockInstruction() [("->" | ";")]
    | BlockInstruction()
}

void NonBlockInstruction() #void : {}
{
    /* variable++ and variable-- */
    LOOKAHEAD(Increment()) Increment()
    | LOOKAHEAD(Decrement()) Decrement()
    /* variable = some expression */
    | LOOKAHEAD(Assignment()) Assignment()
    /* channel ! arguments and channel ? arguments */
    | LOOKAHEAD(SendStatement()) SendStatement()
    | LOOKAHEAD(ReceiveStatement()) ReceiveStatement()
    /* declarations */
    | LOOKAHEAD(MultiDeclaration()) MultiDeclaration()
    | LOOKAHEAD(SimpleDeclaration()) SimpleDeclaration()
    /* simple expressions */
    | LOOKAHEAD(Expression()) Expression()
    /* other */
    | Break()
    | DoLoop()
    | Else()
    | ForLoop()
    | If()
    | Run()
    | Skip()
}

void SendStatement() : {}
{
    Variable() "!" Expression() ("," Expression())*
}

void ReceiveStatement() : {}
{
    Variable() "?" Variable() ("," Variable())*
}

void Increment() : {}
{
    Variable() "++"
}

void Decrement() : {}
{
    Variable() "--"
}

void Assignment() : {}
{
    Variable() "=" Expression()
}

void Break() : {}
{
    <BREAK>
}

void DoLoop() : {}
{
    <DO> ("::" InstructionList())+ <OD>
}

void ForLoop() : {}
{
    <FOR> "(" Variable() ":" Expression() ".." Expression() ")"
    "{" InstructionList() "}"
}

void If() : {}
{
    <IF> ("::" InstructionList())+ <FI>
}

void Else() : {}
{
    <ELSE>
}

void Run() : {}
{
    <RUN> <IDENTIFIER> "(" [Expression() ("," Expression())*] ")"
}

void Skip() : {}
{
    <SKIP_>
}

void Variable() : {Token identifier;}
{
    identifier = <IDENTIFIER> { jjtThis.name = identifier.image; }
    [ "[" Expression() "]" ] [ "." Variable() ]
}

void Expression() : {int numberOfTerms; boolean isBinaryExpression;
String operator;}
{
    //Setup
    {
        numberOfTerms = 1;
        isBinaryExpression = false;
        operator = null;
    }
    //Definition
    Term() (
        "+" Term() {++numberOfTerms; isBinaryExpression = true; operator = "+";}
        | "-" Term() {++numberOfTerms; isBinaryExpression = true; operator = "-";}
        /* Comparative */
        | ">" Term() {++numberOfTerms; isBinaryExpression = true; operator = ">";}
        | ">=" Term() {++numberOfTerms; isBinaryExpression = true; operator = ">=";}
        | "<" Term() {++numberOfTerms; isBinaryExpression = true; operator = "<";}
        | "<=" Term() {++numberOfTerms; isBinaryExpression = true; operator = "<=";}
        | "==" Term() {++numberOfTerms; isBinaryExpression = true; operator = "==";}
        | "!=" Term() {++numberOfTerms; isBinaryExpression = true; operator = "!=";}
        /* Logical */
        | "&&" Term() {++numberOfTerms; isBinaryExpression = true; operator = "&&";}
        | "||" Term() {++numberOfTerms; isBinaryExpression = true; operator = "||";}
    )*
    //Finish
    {
        jjtThis.putValue("numberOfTerms", numberOfTerms);
        jjtThis.putValue("isBinaryExpression", isBinaryExpression);
        jjtThis.putValue("operator", operator);
    }
}

void Term() : {int numberOfFactors; boolean isBinaryExpression;
String operator;}
{
    //Setup
    {
        numberOfFactors = 1;
        isBinaryExpression = false;
        operator = null;
    }
    //Definition
    Factor() (
        "*" Factor() {++numberOfFactors; isBinaryExpression = true; operator = "*";}
        | "/" Factor() {++numberOfFactors; isBinaryExpression = true; operator = "/";}
    )*
    //Finish
    {
        jjtThis.putValue("numberOfFactors", numberOfFactors);
        jjtThis.putValue("isBinaryExpression", isBinaryExpression);
        jjtThis.putValue("operator", operator);
    }
}

void Factor() : {Token literal; String factorType = null;
    String factorValue = null;}
{
    (
    //It might contain an expression enclosed in parentheses
    "(" Expression() ")" { factorType = "expressionParentheses"; }
    //Literals and constants go here
    | literal = <INTEGER_LITERAL> { factorType = "integerLiteral"; factorValue = literal.image; }
    | literal = <STRING_LITERAL> { factorType = "stringLiteral"; factorValue = literal.image; }
    | literal = <TRUE> { factorType = "booleanConstant"; factorValue = "true"; }
    | literal = <FALSE> { factorType = "booleanConstant"; factorValue = "false"; }
    | literal = <TIMEOUT> { factorType = "timeout"; factorValue = "timeout"; }
    //Everything else is either a function call or a variable
    | LOOKAHEAD(<IDENTIFIER> "(") FunctionCall() { factorType = "functionCall"; }
    | Variable() { factorType = "variable"; }
    )
    //Save the information about our factor
    {
        jjtThis.putValue("factorType", factorType);
        jjtThis.putValue("factorValue", factorValue);
    }
}

void FunctionCall() : {Token functionName;}
{
    functionName = <IDENTIFIER>
    { jjtThis.putValue("functionName", functionName.image); }
    /* Handle the select function as a special case */
    (LOOKAHEAD({token.image.equals("select")}) "(" Variable() ":" Expression()
    ".." Expression() ")"
    /* Every other function should be the same */
    | "(" [FunctionCallParameters()] ")")
}

void FunctionCallParameters() : {}
{
    Expression() ("," Expression())*
}

void BlockInstruction() #void : {}
{
    AtomicBlock()
    | DStepBlock()
}

void AtomicBlock() : {}
{
    <ATOMIC> "{" InstructionList() "}"
}

void DStepBlock() : {}
{
    <D_STEP> "{" InstructionList() "}"
}

void ProcessDefinition() : {Token processName;}
{
    <PROCTYPE> processName = <IDENTIFIER>
    { jjtThis.putValue("processName", processName.image); }
    "(" /* ProcessParameters() */ ")"
    "{" InstructionList() "}"
}

void InitProcessDefinition() : {}
{
    <INIT> "{" InstructionList() "}"
}